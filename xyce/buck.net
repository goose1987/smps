* C:\Users\hpham\Documents\scratch\buck.asc
* high side andd low side mosfet in synchronous buck
XSWMAIN VG1 VDMAIN SW EPC2010C
XSWSYNC VG2 SW SSOURCE EPC2010C
* gate drive resistors
RGMAIN VG1 VHSDRV 1
RGSYNC VG2 VLSDRV 1
* R from vin to drain main fet to measure current
RMAIN VIN VDMAIN 0
*R from sync fet source to ground to measure current
RSYNC SSOURCE 0 0

L1 SW VOUT {LOUT}
C1 VOUT 0 {COUT}
R1 VOUT 0 {RLOAD}
V1 N001 0 PULSE(0 5 1u 10n 10n {TON} 2u)

V2 VIN 0 {VIN}

XU1 DVCC 0 N001 VHSDRV SW VLSDRV PWMinterlock

V3 DVCC 0 5

.PARAM RLOAD =2
.PARAM COUT=100e-6
.PARAM LOUT=10e-6
.PARAM VIN=80
.PARAM TON= 500e-9


* include miscellaneous library for pwm circuit
.include misc.lib
* include modified egan library
.include EPCGaN.lib

* transient simulation to 200usec, start saving data at 100usec
.tran 1E-10 1m 800u 1
.measure tran POWER_SYNCFET AVG {V(SW)*I(RSYNC)} FROM=800e-6
.measure tran POWER_MAINFET AVG {(V(VDMAIN)-V(SW))*I(RMAIN)} FROM=800e-6
.measure tran POWER_FETS AVG {V(SW)*I(RSYNC)+(V(VDMAIN)-V(SW))*I(RMAIN)} FROM = 800e-6
.measure tran VDSMFET MAX {V(VDMAIN)-V(SW)}
.measure tran VDSSFET MAX {V(SW)}
.measure tran IDMFET MAX {ABS(I(RMAIN))}
.measure tran IDSFET MAX {ABS(I(RSYNC))}
* print result to csv
.print tran format=csv V(SW) V(VG1) V(VG2) I(RSYNC) I(RMAIN)
.end
