* C:\Users\hpham\Documents\scratch\buck.asc
* high side andd low side mosfet in synchronous buck
XSWMAIN VG1 VDMAIN SW EPC2010C
XSWSYNC VG2 SW SSOURCE EPC2010C
* gate drive resistors
RGMAIN VG1 VHSDRV 0
RGSYNC VG2 VLSDRV 0
* R from vin to drain main fet to measure current
RMAIN VIN VDMAIN 0
*R from sync fet source to ground to measure current
RSYNC SSOURCE 0 0

L1 SW VOUT {LOUT}
C1 VOUT 0 {COUT}
R1 VOUT 0 {RLOAD}
V1 N001 0 PULSE(0 5 1u 10n 10n {TON} 2u)

V2 VIN 0 {VIN}

XU1 DVCC 0 N001 VHSDRV SW VLSDRV PWMinterlock

V3 DVCC 0 5

.PARAM RLOAD =4.666
.PARAM COUT=10e-6
.PARAM LOUT=10e-6
.PARAM VIN=100
.PARAM TON= {(32/VIN)*2e-6}


* include miscellaneous library for pwm circuit
.include misc.lib
* include modified egan library
.include EPCGaN.lib

* transient simulation to 200usec, start saving data at 100usec
.tran 1E-12 2m 1.8m 1
.measure tran POWER_MAINFET AVG {(V(VDMAIN)-V(SW))*I(RMAIN)} FROM=1.8e-3
.measure tran POWER_SYNCFET AVG {V(SW)*I(RSYNC)} FROM=1.8e-3
.measure tran POWER_FETS AVG {V(SW)*I(RSYNC)+(V(VDMAIN)-V(SW))*I(RMAIN)} FROM = 1.8e-3
.measure tran VDSMFET MAX {V(VDMAIN)-V(SW)} FROM=1.8e-3
.measure tran VDSSFET MAX {V(SW)} FROM=1.8e-3
.measure tran IDMFET AVG {ABS(I(RMAIN))} FROM = 1.8e-3
.measure tran IDSFET AVG {ABS(I(RSYNC))} FROM = 1.8e-3
.measure tran IOUT AVG {I(R1)} FROM = 1.8e-3
.measure tran VOUT AVG {V(VOUT)} FROM = 1.8e-3

* print result to csv
.print tran format=csv {(V(VDMAIN)-V(SW))} {I(RMAIN)} V(VG1) V(VG2)
.end
